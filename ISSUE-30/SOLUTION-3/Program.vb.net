Imports System
Imports System.Collections.Generic
Imports System.Text

Module Program

    Dim lookup As List(Of KeyValuePair(Of Integer, String))
    Dim output As StringBuilder

    Sub Main()

        ' Populate the lookup list
        lookup = PopulateLookup()
        output = New StringBuilder()

        Do
            ' Ask the user for a decimal number
            Console.Write("Enter a decimal number to convert into Roman numerals (Enter to quit) : ")
            Dim input As String = Console.ReadLine()
            If String.IsNullOrEmpty(input) Then Exit Do

            output.Clear()

            ' Parse the string into an integer.  If this fails, show a message to tell the
            ' user to try again.
            Dim number As Integer
            If Integer.TryParse(input, number) Then

                ' Romans had no concept of zero or anything larger than a few thousand
                If number > 0 AndAlso number <= 4000 Then

                    Dim inputNumber As Integer = number

                    ' Work out the roman numerals
                    For i As Integer = 0 To lookup.Count - 1

                        While number >= lookup(i).Key

                            output.Append(lookup(i).Value)
                            number -= lookup(i).Key

                        End While
                    Next
                    Console.WriteLine("{0} is {1} in Roman numerals", inputNumber, output.ToString())
                Else
                    Console.WriteLine("Decimal number must be between 1 and 4000")
                End If
            Else
                Console.WriteLine("Invalid input.")
            End If
            Console.WriteLine()
        Loop    ' Keep going until we explicitly exit the loop

    End Sub

    ''' <summary>
    ''' We'll use a lookup table to do the conversion
    ''' </summary>
    ''' <returns>A list of key/value pairs that map integer values to a roman numeral(s)
    ''' </returns>
    ''' <remarks></remarks>
    Private Function PopulateLookup() As List(Of KeyValuePair(Of Integer, String))

        Dim lookup As New List(Of KeyValuePair(Of Integer, String))
        lookup.Add(New KeyValuePair(Of Integer, String)(1000, "M"))
        lookup.Add(New KeyValuePair(Of Integer, String)(900, "CM"))
        lookup.Add(New KeyValuePair(Of Integer, String)(500, "D"))
        lookup.Add(New KeyValuePair(Of Integer, String)(400, "CD"))
        lookup.Add(New KeyValuePair(Of Integer, String)(100, "C"))
        lookup.Add(New KeyValuePair(Of Integer, String)(90, "XC"))
        lookup.Add(New KeyValuePair(Of Integer, String)(50, "L"))
        lookup.Add(New KeyValuePair(Of Integer, String)(40, "XL"))
        lookup.Add(New KeyValuePair(Of Integer, String)(10, "X"))
        lookup.Add(New KeyValuePair(Of Integer, String)(9, "IX"))
        lookup.Add(New KeyValuePair(Of Integer, String)(5, "V"))
        lookup.Add(New KeyValuePair(Of Integer, String)(4, "IV"))
        lookup.Add(New KeyValuePair(Of Integer, String)(1, "I"))

        Return lookup

    End Function

End Module
